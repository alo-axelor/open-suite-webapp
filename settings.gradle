pluginManagement {
  repositories {
    mavenCentral() {
      content {
        excludeGroup 'com.axelor'
      }
    }
    maven {
      url 'https://repository.axelor.com/nexus/repository/maven-public/'
    }
  }
  plugins {
    id 'com.axelor.app' version '6.1.+'
  }
}


dependencyResolutionManagement {
  repositories {
    mavenCentral() {
      content {
        excludeGroup 'com.axelor'
      }
    }
    maven {
      url 'https://repository.axelor.com/nexus/repository/maven-public/'
    }
    // Declare the Node.js download repository
    ivy {
      name = "Node.js"
      setUrl("https://nodejs.org/dist/")
      patternLayout {
        artifact("v[revision]/[artifact](-v[revision]-[classifier]).[ext]")
      }
      metadataSources {
        artifact()
      }
      content {
        includeModule("org.nodejs", "node")
      }
    }
  }
}

//Include all modules
rootProject.name = "axelor-erp"

def modules = []
file("modules").traverse(type: groovy.io.FileType.DIRECTORIES, maxDepth: 1) { it ->
  if (new File(it, "build.gradle").exists()) {
    modules.add(it)
  }
}

gradle.ext.appModules = modules

modules.each { dir ->
  include "modules:$dir.name"
  project(":modules:$dir.name").projectDir = dir
}

def requiredModules = [
        "axelor-account",
        "axelor-bank-payment",
        "axelor-base",
        "axelor-business-production",
        "axelor-business-project",
        "axelor-business-support",
        "axelor-cash-management",
        "axelor-client-portal",
        "axelor-contract",
        "axelor-crm",
        "axelor-fleet",
        "axelor-gdpr",
        "axelor-helpdesk",
        "axelor-human-resource",
        "axelor-maintenance",
        "axelor-marketing",
        "axelor-mobile-settings",
        "axelor-production",
        "axelor-project",
        "axelor-project-dms",
        "axelor-purchase",
        "axelor-quality",
        "axelor-sale",
        "axelor-stock",
        "axelor-supplier-management",
        "axelor-supplier-portal",
        "axelor-supplychain",
        "axelor-talent"
]

gradle.ext.requiredModules = requiredModules
